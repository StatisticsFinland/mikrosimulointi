/*******************************************************************
*  Kuvaus: Sairausvakuutuksen päivärahojen lainsäädäntöä makroina  *
*******************************************************************/


/* 1. SISÄLLYS */

/* Tiedosto sisältää seuraavat makrot */

/*
2.1 SairVakPrahaK1 = Sairausvakuutuksen päivärahan laskukaava, versio 1 helmikuuhun 1983 asti
2.2 SairVakPrahaK2 = Sairausvakuutuksen päivärahan laskukaava, versio 2 joulukuuhun 1983 asti
2.3 SairVakPrahaK3 = Sairausvakuutuksen päivärahan laskukaava, versio 3 joulukuuhun 1991 asti
2.4 SairVakPrahaK4 = Sairausvakuutuksen päivärahan laskukaava, versio 4 elokuuhun 1992 asti
2.5 SairVakPrahaK5 = Sairausvakuutuksen päivärahan laskukaava, versio 5 joulukuuhun 1995 asti
2.6 SairVakPrahaK6 = Sairausvakuutuksen päivärahan laskukaava, versio 6 tammikuusta 1996 lähtien
2.7 SairVakPrahaKS = Sairausvakuutuksen päiväraha kuukausitasolla
2.8 SairVakPrahaVS = Sairausvakuutuksen päiväraha kuukausitasolla vuosikeskiarvona
3. SairVakTuloKS = Sairausvakuutuksen päivärahan perusteena oleva vuositulo (käänteismakro)
4. SairVakTuloVS = Sairausvakuutuksen päivärahan perusteena oleva vuositulo vuosikeskiarvona (käänteismakro)
5. HarkPRahaS = Tarveharkintainen sairausvakuutuksen päiväraha (1996 - 2002) kuukausitasolla
6. KorVanhRahaKS = Korotettu vanhempainpäiväraha kuukausitasolla
7. KorVanhRahaVS = Korotettu vanhempainpäiväraha kuukausitasolla vuosikeskiarvona
8. VanhPRahaKS = Eri suuruiset vanhempainpäivärahat päivää kohden
9. VanhPRahaVS = Eri suuruiset vanhempainpäivärahat päivää kohden vuosikeskiarvona
10. VanhRahaTuloS = Vanhempainpäivärahojen perusteena oleva tulo, kun koko päivärahatulo ja erilaisten tasojen päivät tiedetään
*/


/* 2. Kuusi sairausvakuutuksen päivärahan laskumakroa lainsäädännön muutosten perusteella.
      Tässä ei vielä oteta huomioon lapsikorotuksia. 
	  Näissä kaavoissa ei myöskään vielä tarvita kerrointa, joilla 
      laskennan perusteena olevia työtuloja alennetaan vuodesta 1993 lähtien.
      Kaavat laskevat päivärahan kuukausitasolla (25 * päiväarvo)  */

*Päivärahamakrojen parametrit:
tulos: Makron tulosmuuttuja, e/kk 
mvuosi: Vuosi, jonka lainsäädäntöä käytetään
mkuuk: Kuukausi, jonka lainsäädäntöä käytetään 
minf: Deflaattori euromääräisten parametrien kertomiseksi 
vanh: Onko vanhempainpäiväraha (=1) tai ei (=0)
lapsia: Alaikäisten lasten lukumäärä (ei vaikutusta vuoden 1993 jälkeen, parametrin arvo parametritaulukossa ratkaisee)
tulo: Henkilön omat (työ)tulot, e/vuosi
yrittaja: Henkilön yrittäjätulot, e/vuosi
tulonhankk: Tuloverolain 93 - 95 §:n mukaiset tulonhankkimiskulut;


/* 2.1 Laskukaava helmikuuhun 1983 asti */

%MACRO SairVakPrahaK1 (tulos, tulo)/
DES = 'SAIRVAK: Sairausvakuutuksen päivärahan laskukaava, versio 1 helmikuuhun 1983 asti';

temp = &SPros1 * &tulo / &maxpaiv;
temp =  &Minimi + temp;

IF (temp < &SPros2 * &tulo / &maxpaiv) THEN temp = &SPros2 * &tulo / &maxpaiv;

temp = &SPaivat * temp;
&tulos = temp;
DROP temp;
%MEND SairVakPrahaK1;

/* 2.2 Laskukaava joulukuuhun 1983 asti */

%MACRO SairVakPrahaK2 (tulos, tulo)/
DES = 'SAIRVAK: Sairausvakuutuksen päivärahan laskukaava, versio 2 joulukuuhun 1983 asti';

temp = &SPros1 * &tulo / &maxpaiv;
temp =  &Minimi + temp;

IF (temp < &SPros2 * &tulo / &maxpaiv) THEN temp = &SPros2 * &tulo / &maxpaiv;
IF (temp <  &Minimi) THEN temp =  &Minimi;
IF (&tulo >  &SRaja1) THEN temp = (&SPros2 *  &SRaja1 + &SPros3 * (&tulo -  &SRaja1)) / &maxpaiv;

temp = &SPaivat * temp;
&tulos = temp;
DROP temp;
%MEND SairVakPrahaK2;

/* 2.3 Laskukaava joulukuuhun 1991 asti */

%MACRO SairVakPrahaK3 (tulos, tulo)/
DES = 'SAIRVAK: Sairausvakuutuksen päivärahan laskukaava, versio 3 joulukuuhun 1991 asti';

temp = &SPros1 * &tulo/&maxpaiv;
temp =  &Minimi + temp;

IF (temp < &SPros2 * &tulo / &maxpaiv) THEN temp = &SPros2 * &tulo / &maxpaiv;
IF (&tulo >  &SRaja1) THEN temp = (&SPros2 *  &SRaja1 + &SPros3 * (&tulo -  &SRaja1)) / &maxpaiv;
IF (&tulo >  &SRaja2) THEN temp = (&SPros2 *  &SRaja1 + &SPros3*  (&SRaja2 - &SRaja1)
	+ &SPros4 * (&tulo -  &SRaja2)) / &maxpaiv;

temp = &SPaivat * temp;
&tulos = temp;
DROP temp;
%MEND SairVakPrahaK3;

/* 2.4 Laskukaava elokuuhun 1992 asti */

%MACRO SairVakPrahaK4 (tulos, tulo)/
DES = 'SAIRVAK: Sairausvakuutuksen päivärahan laskukaava, versio 4 elokuuhun 1992 asti';

temp = &SPros1 * &tulo/&maxpaiv;
temp =  &Minimi + temp;

IF (temp < &SPros2 * &tulo / &maxpaiv) THEN temp = &SPros2 * &tulo / &maxpaiv;
IF (&tulo >  &SRaja1) THEN temp = (&SPros2 *  &SRaja1+ &SPros3 * (&tulo -  &SRaja1)) / &maxpaiv;
IF (&tulo >  &SRaja2) THEN temp = (&SPros2 *  &SRaja1 + &SPros3 *  (&SRaja2 - &SRaja1)
	+ &SPros4 * (&tulo -  &SRaja2)) / &maxpaiv;
IF (&tulo >  &SRaja3) THEN temp = (&SPros2 *  &SRaja1 + &SPros3 *  (&SRaja2 - &SRaja1)
	+ &SPros4 * (&SRaja3 - &SRaja2) + &SPros5 * (&tulo -  &SRaja3)) / &maxpaiv;

temp = &SPaivat * temp;
&tulos = temp;
DROP temp;
%MEND SairVakPrahaK4;

/* 2.5 Laskukaava joulukuuhun 1995 asti */

%MACRO SairVakPrahaK5 (tulos, mvuosi, vanh, tulo)/
DES = 'SAIRVAK: Sairausvakuutuksen päivärahan laskukaava, versio 5 joulukuuhun 1995 asti';

temp =  &Minimi + &SPros1 * &tulo / &maxpaiv;

IF &tulo >  &SRaja1 THEN DO;
	temp =  &Minimi + &SPros1 * &SRaja1 / &maxpaiv;
	temp = temp + &SPros2 * (&tulo -  &SRaja1) / &maxpaiv;
END;
IF (&tulo >  &SRaja2) THEN DO;
	temp =  &Minimi + &SPros1 *  &SRaja1 / &maxpaiv + &SPros2 *  (&SRaja2 - &SRaja1) / &maxpaiv;
	temp = temp + &SPros3 * (&tulo -  &SRaja2) / &maxpaiv;
END;
IF (&tulo >  &SRaja3) THEN DO;
	temp =  &Minimi + &SPros1 *  &SRaja1 / &maxpaiv + &SPros2*  (&SRaja2 - &SRaja1) / &maxpaiv 
		+ &SPros3 *  (&SRaja3 - &SRaja2) / &maxpaiv;
	temp = temp + &SPros4 * (&tulo -  &SRaja3) / &maxpaiv;
END;
IF &vanh NE 0 THEN DO;
	IF (temp <  &VanhMin) THEN  temp = &VanhMin;
END;

*Poikkeukselliset pienten päivärahojen korotukset;
*Huom! Vuonna 1994 kriteerinä päiväraha ja 1995 tulo;
IF (&mvuosi = 1994) AND  (temp <  &PoikRaja1) THEN temp = (1 + &PoikPros) * temp;
IF (&mvuosi = 1995) AND  (&tulo <  &PoikRaja2) THEN temp = (1 + &PoikPros) * temp;

temp = &SPaivat * temp;
&tulos = temp;
DROP temp;
%MEND SairVakPrahaK5;

/* 2.6 Laskukaava tammikuusta 1996 lähtien */
/*Huom vanhempainpäivärahojen poikkeavat parametrit SRaja2Vanh, SPros2Vanh ja SPros3Vanh*/

%MACRO SairVakPrahaK6 (tulos, mvuosi, vanh, tulo)/
DES = 'SAIRVAK: Sairausvakuutuksen päivärahan laskukaava, versio 6 tammikuusta 1996 lähtien';

*Ensin tilanne, jossa ei ole kyse vanhempainrahasta;
IF &vanh = 0 THEN DO;
	IF (&tulo <  &SRaja1) THEN &tulos = 0;
	IF (&tulo >=  &SRaja1) THEN &tulos = &SPros1 * &tulo / &maxpaiv;
	IF (&tulo >  &SRaja2) THEN &tulos = &SPros1 *  &SRaja2 / &maxpaiv + &SPros2 * (&tulo -  &SRaja2) / &maxpaiv;
	IF (&tulo >  &SRaja3) THEN &tulos = &SPros1 *  &SRaja2 / &maxpaiv + &SPros2 *  (&SRaja3 -&SRaja2) / &maxpaiv + &SPros3 * (&tulo -  &SRaja3) / &maxpaiv;
	IF (&tulos < &Minimi) AND (&mvuosi > 2018) THEN &tulos = &Minimi;
END;

*Sitten vanhempainraha;
IF &vanh NE 0 THEN DO;
	IF (&tulo <  &SRaja1) THEN &tulos = 0;
	IF (&tulo >=  &SRaja1) THEN &tulos = &SPros1 * &tulo / &maxpaiv;
	IF (&tulo > &SRaja2Vanh) THEN &tulos = &SPros1 * &SRaja2Vanh /&maxpaiv + &SPros2Vanh * (&tulo -  &SRaja2Vanh) / &maxpaiv;
	IF (&tulo >  &SRaja3) THEN &tulos = &SPros1 *  &SRaja2Vanh / &maxpaiv + &SPros2Vanh *  (&SRaja3 -&SRaja2Vanh) / &maxpaiv + &SPros3Vanh * (&tulo -  &SRaja3) / &maxpaiv;
	IF (&tulos <  &VanhMin) THEN &tulos =  &VanhMin;
END;

&tulos = &SPaivat * &tulos;
%MEND SairVakPrahaK6;


/* 2.7 Makro laskee sairausvakuutuksen päivärahan kuukausitasolla valitsemalla ajankohdan mukaan jonkin edellisistä makroista. 
	   Tässä vaiheessa lisätään (mahdolliset) lapsikorotukset.
	   Kerroin, jolla työtuloa alennetaan, otetaan myös tässä huomioon */

%MACRO SairVakPrahaKS (tulos, mvuosi, mkuuk, minf, vanh, lapsia, tulo, yrittaja=0, tulonhankk=0)/
DES = 'SAIRVAK: Sairausvakuutuksen päiväraha kuukausitasolla';

%HaeParam&TYYPPI(&mvuosi, &mkuuk, &SAIRVAK_PARAM, PARAM.&PSAIRVAK);
%ParamInf&TYYPPI(&mvuosi, &mkuuk, &SAIRVAK_MUUNNOS, &minf);

%LuoKuuID(kuuid, &mvuosi, &mkuuk);

IF &mvuosi <= 2019 THEN DO;
	tyotulo1 = MAX(0, SUM(&tulo, -&PalkVah*&tulo, -&tulonhankk));
	tyotulo2 = MAX(0, &yrittaja);
END;
ELSE DO;
	tyotulo1 = MAX(0, SUM(&tulo, -&PalkVah*&tulo));
	tyotulo2 = MAX(0, &yrittaja);
END;

IF &mvuosi < 2001 OR (&mvuosi = 2001 AND &mkuuk < 7) THEN DO;
	tyotulo2 = 0;
END;

tyotulo = SUM(tyotulo1, tyotulo2);

IF tyotulo < 0 THEN tyotulo = 0;
lapsluku = &lapsia;
IF lapsluku > &SMaksLaps THEN lapsluku = &SMaksLaps;
lapsikorot = &SPaivat * lapsluku *  &LapsiKor;

*Ennen maaliskuuta 1983;
IF kuuid < MDY(3, 1, 1983) THEN DO;
	%SairVakPrahaK1(temp, tyotulo)
END;

*Ennen tammikuuta 1984;
IF kuuid >= MDY(3, 1, 1983) AND  kuuid < MDY(1, 1, 1984) THEN DO;
	%SairVakPrahaK2(temp,   tyotulo);
END;

*Ennen tammikuuta 1992;
IF kuuid >= MDY(1, 1, 1984) AND  kuuid < MDY(1, 1, 1992) THEN DO;
	%SairVakPrahaK3(temp,   tyotulo);
END;

*Ennen syyskuuta 1992;
IF kuuid >= MDY(1, 1, 1992) AND  kuuid < MDY(9, 1, 1992) THEN DO;
	%SairVakPrahaK4(temp,    tyotulo);
END;

*Ennen tammikuuta 1996;
IF kuuid >= MDY(9, 1, 1992) AND  kuuid < MDY(1, 1, 1996) THEN DO;
	%SairVakPrahaK5(temp, &mvuosi,   &vanh, tyotulo);
END;

*Tammikuusta 1996 lähtien;
IF kuuid >= MDY(1, 1, 1996) THEN DO;
	%SairVakPrahaK6(temp, &mvuosi, &vanh, tyotulo);
END;

&tulos = temp + lapsikorot;
DROP kuuid temp tyotulo1 tyotulo2 tyotulo lapsluku lapsikorot ;
%MEND SairVakPrahaKS;


/* 2.8. Makro laskee sairausvakuutuksen päivärahan kuukausitasolla vuosikeskiarvona */

%MACRO SairVakPrahaVS (tulos, mvuosi, minf, vanh, lapsia, tulo, yrittaja = 0, tulonhankk = 0)/
DES = 'SAIRVAK: Sairausvakuutuksen päiväraha kuukausitasolla vuosikeskiarvona';

raha = 0;

%DO i = 1 %TO 12;
	%SairVakPrahaKS(temp, &mvuosi, &i, &minf, &vanh, &lapsia,  &tulo, yrittaja = &yrittaja, tulonhankk = &tulonhankk);
	raha = raha + temp;
%END;

&tulos = raha / 12;
DROP raha temp;
%MEND SairVakPrahaVS;


/* 3. Käänteismakro, jonka avulla voidaan päätellä päivärahan perusteena oleva vuositulo */

*Makron parametrit:
tulos: Makron tulosmuuttuja, sairausvakuutuksen päivärahan perusteena oleva tulo, e/vuosi 
mvuosi: Vuosi, jonka lainsäädäntöä käytetään
mkuuk: Kuukausi, jonka lainsäädäntöä käytetään 
minf: Deflaattori euromääräisten parametrien kertomiseksi 
vanh: Onko vanhempainpäiväraha (=1) tai ei (=0)
lapsia: Alaikäisten lasten lukumäärä (ei vaikutusta vuoden 1993 jälkeen, parametrin arvo parametritaulukossa ratkaisee)
praha: Sairausvakuutuksen päiväraha,;

%MACRO SairVakTuloKS(tulos, mvuosi, mkuuk, minf, vanh, lapsia, praha)/
DES = 'SAIRVAK: Sairausvakuutuksen päivärahan perusteena oleva vuositulo (käänteismakro)';

%SairVakPRahaKS(testi, &mvuosi, &mkuuk, &minf, &vanh, &lapsia,  0);

IF &praha <= testi THEN &tulos = 0;
	ELSE DO;
		DO i = 1 TO 100 UNTIL(testi >= &praha);
			%SairVakPrahaKS(testi, &mvuosi, &mkuuk, &minf, &vanh, &lapsia,  i * 10000);
		END;
		DO j = -9 TO 9 UNTIL(testi >= &praha);
			%SairVakPrahaKS(testi, &mvuosi, &mkuuk, &minf, &vanh, &lapsia,  (i * 10000 + j * 1000) );
		END;
		DO k = -9 TO 9 UNTIL(testi >= &praha);
			%SairVakPrahaKS(testi, &mvuosi, &mkuuk, &minf, &vanh, &lapsia,  (i * 10000 + j * 1000 + k * 100));
		END;
		DO m = -9 TO 9 UNTIL(testi >= &praha);
			%SairVakPrahaKS(testi, &mvuosi, &mkuuk, &minf, &vanh, &lapsia,  (i * 10000 + j * 1000 + k * 100 + m * 10));
		END;
		DO n = -9 TO 9 UNTIL(testi >= &praha);
			%SairVakPrahaKS(testi, &mvuosi, &mkuuk, &minf, &vanh, &lapsia,  (i * 10000 + j * 1000 + k * 100 + m * 10 + n ));
		END;
		DO p = -9 TO 9 UNTIL(testi >= &praha);
			%SairVakPrahaKS(testi, &mvuosi, &mkuuk, &minf, &vanh, &lapsia, (i * 10000 + j * 1000 + k * 100 + m * 10 + n + p / 10));
		END;
	&tulos = (i * 10000 + j * 1000 + k * 100 + m * 10 + n + p / 10);
END;

&tulos =&tulos;
DROP i j k m n p testi;
%MEND SairVakTuloKS;


/* 4. Makro, jonka avulla voidaan päätellä päivärahan perusteena oleva vuositulo vuosikeskiarvona */

*Makron parametrit:
tulos: Makron tulosmuuttuja, päivärahan perusteena oleva tulo, e/vuosi (vuosikeskiarvo)
mvuosi: Vuosi, jonka lainsäädäntöä käytetään
mkuuk: Kuukausi, jonka lainsäädäntöä käytetään 
minf: Deflaattori euromääräisten parametrien kertomiseksi 
vanh: Onko vanhempainpäiväraha (=1) tai ei (=0)
lapsia: Alaikäisten lasten lukumäärä (ei vaikutusta vuoden 1993 jälkeen, parametrin arvo parametritaulukossa ratkaisee)
praha: Sairausvakuutuksen päiväraha, e/kk;

%MACRO SairVakTuloVS(tulos, mvuosi, minf, vanh, lapsia, praha)/
DES = 'SAIRVAK: Sairausvakuutuksen päivärahan perusteena oleva vuositulo (käänteismakro) vuosikeskiarvona';

%SairVakPrahaVS(testi, &mvuosi, &minf, &vanh, &lapsia,  0);

IF &praha <= testi THEN &tulos = 0;
	ELSE DO;
		DO i = 1 TO 100 UNTIL(testi >= &praha);
			%SairVakPrahaVS(testi, &mvuosi, &minf, &vanh, &lapsia,  i * 10000);
		END;
		DO j = -9 TO 9 UNTIL(testi >= &praha);
			%SairVakPrahaVS(testi, &mvuosi, &minf, &vanh, &lapsia,  (i * 10000 + j * 1000) );
		END;
		DO k = -9 TO 9 UNTIL(testi >= &praha);
			%SairVakPrahaVS(testi, &mvuosi, &minf, &vanh, &lapsia,  (i * 10000 + j * 1000 + k * 100));
		END;
		DO m = -9 TO 9 UNTIL(testi >= &praha);
			%SairVakPrahaVS(testi, &mvuosi, &minf, &vanh, &lapsia,  (i * 10000 + j * 1000 + k * 100 + m * 10));
		END;
		DO n = -9 TO 9 UNTIL(testi >= &praha);
			%SairVakPrahaVS(testi, &mvuosi, &minf, &vanh, &lapsia,  (i * 10000 + j * 1000 + k * 100 + m * 10 + n ));
		END;
		DO p = -9 TO 9 UNTIL(testi >= &praha);
			%SairVakPrahaVS(testi, &mvuosi, &minf, &vanh, &lapsia, (i * 10000 + j * 1000 + k * 100 + m * 10 + n + p / 10));
		END;
	&tulos = (i * 10000 + j * 1000 + k * 100 + m * 10 + n + p / 10);
END;

&tulos = &tulos;
DROP i j k m n p testi;
%MEND SairVakTuloVS;


/* 5. Makro laskee vuosina 1996 - 2002 sovelletun tarveharkintaisen sairausvakuutuksen päivärahan kuukausitasolla. 
	  Jos harkinnan parametrit eivät ole voimassa, makro tuottaa minipäivärahan */

*Makron parametrit:
tulos: Makron tulosmuuttuja, tarveharkintainen päiväraha, e/kk
mvuosi: Vuosi, jonka lainsäädäntöä käytetään
mkuuk: Kuukausi, jonka lainsäädäntöä käytetään 
minf: Deflaattori euromääräisten parametrien kertomiseksi 
tulo: Henkilön omat tulot, e/kk
puoltulo: Puolison tulot, e/kk 
varall: Veronalainen varallisuus, e;

%MACRO HarkPRahaS(tulos, mvuosi, mkuuk, minf, tulo, puoltulo, varall)/
DES = 'SAIRVAK: Tarveharkintainen sairausvakuutuksen päiväraha (1996 - 2002) kuukausitasolla';

%HaeParam&TYYPPI(&mvuosi, &mkuuk, &SAIRVAK_PARAM, PARAM.&PSAIRVAK);
%ParamInf&TYYPPI(&mvuosi, &mkuuk, &SAIRVAK_MUUNNOS, &minf);

%LuoKuuID(kuuid, &mvuosi, &mkuuk);

temp = &Minimi;
temp = &Minimi - &HarkRaja * &tulo - &HarkPuol  * &puoltulo;
IF temp < 0 THEN temp = 0;

IF kuuid < MDY(4, 1, 2002) AND  kuuid >= MDY(1, 1, 1996) THEN DO;
	IF &varall >  &VarRaja THEN temp = 0;
END;

&tulos = &SPaivat * temp;
DROP temp kuuid;
%MEND HarkPRahaS;


/* 6. Makro laskee vuonna 2007 käyttöön otetun korotetun vanhempainpäivärahan kuukausitasolla */

*Makron parametrit:
tulos: Makron tulosmuuttuja, korotettu vanhempainpäiväraha, e/kk
mvuosi: Vuosi, jonka lainsäädäntöä käytetään
mkuuk: Kuukausi, jonka lainsäädäntöä käytetään 
minf: Deflaattori euromääräisten parametrien kertomiseksi 
ait: 1)
	 07/2022 asti: äideille ensimmäisten 56 äitiyslomapäivän aikana
     myönnettävä korotettu päiväraha, jossa käytetään 90 prosentin
     kerrointa.
	 08/2022 lähtien: äideille 40 raskauspäivärahapäivän aikana
     myönnettävä korotettu päiväraha, jossa käytetään 90 prosentin
     kerrointa. 
	 0) 2015 asti: 75 prosentin kerroin
		2016-07/2022: 0 prosentin kerroin
		08/2022 lähtien: 90 prosentin kerroin ensimmäiseltä 16 päivältä
lapsia: Alaikäisten lasten lukumäärä (ei vaikutusta vuoden 1993 jälkeen, parametrin arvo parametritaulukossa ratkaisee)
tulo: Henkilön omat (työ)tulot, e/vuosi
yrittaja: Henkilön yrittäjätulot, e/vuosi
tulonhankk: tulonhankkimiskulut;

%MACRO KorVanhRahaKS (tulos, mvuosi, mkuuk, minf, ait, lapsia, tulo, yrittaja = 0, tulonhankk = 0)/
DES = 'SAIRVAK: Korotettu vanhempainpäiväraha kuukausitasolla';

/*Ennen vuotta 2007 lasketaan normaali vanhempainpäiväraha. Samoin välillä 2016-07/2022, jos kyse ei ole äitien
ensimmäisen 56 päivän äitiysrahasta.;*/
IF &mvuosi < 2007 OR ((2015 < &mvuosi < 2022 OR (&mvuosi = 2022 AND &mkuuk < 8)) AND  &ait = 0) THEN DO;
	%SairVakPrahaKS(&tulos, &mvuosi, &mkuuk, &minf, 1, &lapsia, &tulo, yrittaja = &yrittaja, tulonhankk = &tulonhankk);
END;

ELSE DO;
	%HaeParam&TYYPPI(&mvuosi, &mkuuk, &SAIRVAK_PARAM, PARAM.&PSAIRVAK);
	%ParamInf&TYYPPI(&mvuosi, &mkuuk, &SAIRVAK_MUUNNOS, &minf);

	IF &mvuosi <= 2019 THEN DO;
		tyotulo1 = MAX(0, SUM(&tulo, -&PalkVah*&tulo, -&tulonhankk));
		tyotulo2 = MAX(0, &yrittaja);
	END;
	ELSE DO;
		tyotulo1 = MAX(0, SUM(&tulo, -&PalkVah*&tulo));
		tyotulo2 = MAX(0, &yrittaja);
	END;

	IF &mvuosi < 2001 OR (&mvuosi = 2001 AND &mkuuk < 7) THEN DO;
		tyotulo2 = 0;
	END;

	tyotulo = SUM(tyotulo1, tyotulo2);

	IF (tyotulo < 0) THEN tyotulo = 0;
	IF tyotulo <  &SRaja3 THEN DO;
		IF &ait NE 0 THEN temp = &KorProsAit * tyotulo / &maxpaiv;
		IF &ait = 0 THEN temp = &KorPros1 * tyotulo / &maxpaiv;
	END;
	ELSE DO;
		IF &ait NE 0 THEN temp = (&KorProsAit *  &SRaja3 + &KorPros2 * (tyotulo -  &SRaja3)) / &maxpaiv;
		IF &ait = 0 THEN temp = (&KorPros1 *  &SRaja3 + &KorPros2 * (tyotulo -  &SRaja3)) / &maxpaiv;
	END;
	IF temp <  &VanhMin THEN temp =  &VanhMin;
	temp = &SPaivat * temp;
	&tulos = temp;
END;

DROP tyotulo1 tyotulo2 tyotulo temp;
%MEND KorVanhRahaKS;


/* 7. Vuonna 2007 käyttöön otetut korotetut vanhempainrahat kuukausitasolla vuosikeskiarvona */

*Makron parametrit:
tulos: Makron tulosmuuttuja, korotettu vanhempainpäiväraha, e/kk (vuosikeskiarvo)
mvuosi: Vuosi, jonka lainsäädäntöä käytetään
mkuuk: Kuukausi, jonka lainsäädäntöä käytetään 
minf: Deflaattori euromääräisten parametrien kertomiseksi 
ait: (1 tai 0), äideille ensimmäisten 56 äitiyslomapäivän aikana
     myönnettävä korotettu päiväraha, jossa käytetään 90 prosentin
     kerrointa. Muuten kyse on 75 prosentin kertoimesta alkuperäisen
     lainsäädännön mukaan.
lapsia: Alaikäisten lasten lukumäärä (ei vaikutusta vuoden 1993 jälkeen, parametrin arvo parametritaulukossa ratkaisee)
tulo: Henkilön omat (työ)tulot, e/vuosi
yrittaja: Henkilön yrittäjätulot, e/vuosi
tulonhankk: tulonhankkimiskulut;

%MACRO KorVanhRahaVS (tulos, mvuosi,  minf, ait, lapsia, tulo, yrittaja = 0, tulonhankk=0)/
DES = 'SAIRVAK: Korotettu vanhempainpäiväraha kuukausitasolla vuosikeskiarvona';

raha = 0;

%DO i = 1 %to 12;
	%KorVanhRahaKS(temp, &mvuosi, &i, &minf, &ait, &lapsia,  &tulo, yrittaja = &yrittaja, tulonhankk=&tulonhankk);
	raha = raha + temp;
%END;

&tulos = raha / 12;
DROP raha temp;
%MEND KorVanhRahaVS;


/* 8. Makro laskee valinnan mukaan eri tasoiset vanhempainpäivärahat päivää kohden */

*Makron parametrit:
tulos: Makron tulosmuuttuja, vanhempainpäiväraha, e/päivä 
mvuosi: Vuosi, jonka lainsäädäntöä käytetään
mkuuk: Kuukausi, jonka lainsäädäntöä käytetään 
minf: Deflaattori euromääräisten parametrien kertomiseksi 
ait: 1 tai 0, korotettu äitiyspäiväraha 56 ensimmäiseltä päivältä
kor: 1 tai 0, korotettu vanhempainraha
norm: 1 tai 0, normaali päiväraha
lapsia: alaikäisten lasten lukumäärä (ei vaikutusta vuoden 1993 jälkeen, parametrin arvo parametritaulukossa ratkaisee)
vanhtulo: Henkilön omat (työ)tulot, e/vuosi
tulonhankk: tuloverolain mukaiset tulonhankkimiskulut, /vuosi;

%MACRO VanhPRahaKS (tulos, mvuosi, mkuuk, minf, ait, kor, norm, lapsia, vanhtulo, yrittaja =0, tulonhankk=0)/
DES = 'SAIRVAK: Eri suuruiset vanhempainpäivärahat päivää kohden kuukausitasolla';

/*Ennen vuotta 2007 lasketaan normaali päiväraha. Samoin vuodesta 2016 lähtien, jos kyse ei ole äitien
ensimmäisen 56 päivän äitiysrahasta;*/
IF &mvuosi < 2007 or (2015 < &mvuosi AND &PERHEVAP=0 AND &ait = 0) or (&PERHEVAP=1 AND &norm=1) THEN DO;
	%SairVakPrahaKS(temp, &mvuosi, &mkuuk, &minf, 1, &lapsia, &vanhtulo, yrittaja = &yrittaja, tulonhankk=&tulonhankk);
	&tulos = temp / &SPaivat;
END;

*Muussa tapauksessa noudatetaan uutta lainsäädäntöä;
*Ehtolauseiden järjestys merkitsee sitä, että vain ensimmäinen ehdoista (ait, kor, norm) hyväksytään;

ELSE DO;
	%HaeParam&TYYPPI(&mvuosi, &mkuuk, &SAIRVAK_PARAM, PARAM.&PSAIRVAK);
	%ParamInf&TYYPPI(&mvuosi, &mkuuk, &SAIRVAK_MUUNNOS, &minf);

	IF &mvuosi <= 2019 THEN DO;
		tyotulo1 = MAX(0, SUM(&vanhtulo, -&PalkVah*&vanhtulo, -&tulonhankk));
		tyotulo2 = MAX(0, &yrittaja);
	END;
	ELSE DO;
		tyotulo1 = MAX(0, SUM(&vanhtulo, -&PalkVah*&vanhtulo));
		tyotulo2 = MAX(0, &yrittaja);
	END;

	IF &mvuosi < 2001 OR (&mvuosi = 2001 AND &mkuuk < 7) THEN DO;
		tyotulo2 = 0;
	END;

	tyotulo = SUM(tyotulo1, tyotulo2);

	IF &norm = 1 THEN DO;
		IF (tyotulo >  &SRaja3) THEN temp = &SPros1 *  &SRaja2Vanh / &maxpaiv + &SPros2Vanh *  (&SRaja3-&SRaja2Vanh) / &maxpaiv + &SPros3Vanh * (tyotulo -  &SRaja3) / &maxpaiv;
		ELSE IF (tyotulo >  &SRaja2Vanh) THEN temp = &SPros1 *  &SRaja2Vanh / &maxpaiv + &SPros2Vanh * (tyotulo -  &SRaja2Vanh) / &maxpaiv;
		ELSE IF (tyotulo >=  &SRaja1) THEN temp = &SPros1 * tyotulo / &maxpaiv;
		ELSE temp = 0;
	END;
	IF &kor = 1 THEN DO;
		IF (tyotulo <  &SRaja3) THEN temp = &KorPros1 * tyotulo / &maxpaiv;
		ELSE temp = (&KorPros1 *  &SRaja3 + &KorPros2 * (tyotulo -  &SRaja3)) / &maxpaiv;
	END;
	IF &ait = 1 THEN DO;
		IF (tyotulo <  &SRaja3) THEN temp = &KorProsAit * tyotulo / &maxpaiv;
		ELSE temp = (&KorProsAit *  &SRaja3 + &KorPros2 * (tyotulo -  &SRaja3)) / &maxpaiv;
	END;
	IF (temp <  &VanhMin) THEN temp =  &VanhMin;
	&tulos  = temp;
END;

DROP temp tyotulo1 tyotulo2 tyotulo;
%MEND VanhPRahaKS;


/* 9. Makro laskee valinnan mukaan eri tasoiset vanhempainpäivärahat päivää kohden vuosikeskiarvona */

*Makron parametrit:
tulos: Makron tulosmuuttuja, vanhempainpäiväraha, e/päivä 
mvuosi: Vuosi, jonka lainsäädäntöä käytetään
minf: Deflaattori euromääräisten parametrien kertomiseksi 
ait: 1 tai 0, korotettu äitiyspäiväraha 56 ensimmäiseltä päivältä
kor: 1 tai 0, korotettu vanhempainraha
norm: 1 tai 0, normaali päiväraha
lapsia: alaikäisten lasten lukumäärä (ei vaikutusta vuoden 1993 jälkeen, parametrin arvo parametritaulukossa ratkaisee)
vanhtulo: Henkilön omat (työ)tulot, e/vuosi
tulonhankk: tuloverolain mukaiset tulonhankkimiskulut, /vuosi;

%MACRO VanhPrahaVS (tulos, mvuosi, minf, ait, kor, norm, lapsia, vanhtulo, yrittaja=0, tulonhankk=0)/
DES = 'SAIRVAK: Eri suuruiset vanhempainpäivärahat päivää kohden kuukausitasolla vuosikeskiarvona';

raha = 0;

%DO i = 1 %to 12;
	%VanhPrahaKS(temp, &mvuosi, &i, &minf, &ait, &kor, &norm, &lapsia, &vanhtulo, yrittaja=&yrittaja, tulonhankk=&tulonhankk);
	raha = raha + temp;
%END;

&tulos = raha / 12;
DROP raha temp;
%MEND VanhPRahaVS;

/* 10. Makro, joka laskee vanhempainpäivärahan perusteena olevan
      tulon, kun tiedätään koko päivärahatulo ja erilaisten tasojen päivärahapäivät */

*Makron parametrit:
tulos: Makron tulosmuuttuja, vanhempainpäivärahan perusteena oleva tulo
mvuosi: Vuosi, jonka lainsäädäntöä käytetään
normpaiv: Normaalit päivärahapäivät
korpaiv90: Korotetut päivät 90 %:n korvausasteella
korpaiv75: Korotetut päivät 75 %:n korvausasteella
praha: Vanhempainpäiväraha yhteensä;

%MACRO VanhRahaTuloS(tulos, mvuosi, normpaiv, korpaiv90, korpaiv75, praha)/
DES = 'SAIRVAK: Vanhempainpäivärahojen perusteena oleva tulo, kun koko päivärahatulo
ja erilaisten tasojen päivät tiedetään';

*Yli 100 000 euron päivärahatulot sivuutetaan;
IF &praha > 100000 THEN &tulos = 999999;

*Testataan vähimmäispäiväraha;
vahimm = SUM(&normpaiv, &korpaiv90, &korpaiv75) * &VanhMin;

*Vähimmäispäivärahan tapauksessa annetaan tuloksi nolla;
IF &praha <= vahimm THEN &tulos = 0;

*Muussa tapauksessa haarukoidaan päivärahan perusteena oleva tulo;
IF &praha > vahimm THEN DO;
	testi = 0;
	DO i = 1 TO 10 UNTIL(testi >= &praha);
		testitulo = i * 100000;
		%VanhPRahaVS(testi1, &mvuosi, 1, 0, 0, 1, 0, testitulo);
		%VanhPRahaVS(testi2, &mvuosi, 1, 0, 1, 0, 0, testitulo);
		%VanhPRahaVS(testi3, &mvuosi, 1, 1, 0, 0, 0, testitulo);
		testi = &normpaiv * testi1 + &korpaiv75 * testi2 + &korpaiv90 * testi3;
	END;
	DO j = -9 TO 10 UNTIL(testi >= &praha);
		testitulo = (i * 100000 + j * 10000);
		%VanhPRahaVS(testi1, &mvuosi, 1, 0, 0, 1, 0, testitulo);
		%VanhPRahaVS(testi2, &mvuosi, 1, 0, 1, 0, 0, testitulo);
		%VanhPRahaVS(testi3, &mvuosi, 1, 1, 0, 0, 0, testitulo);
		testi = &normpaiv * testi1 + &korpaiv75 * testi2 + &korpaiv90 * testi3;
	END;
	DO k = -9 TO 10 UNTIL(testi >= &praha);
		testitulo = (i * 100000 + j * 10000 + k * 1000);
		%VanhPRahaVS(testi1, &mvuosi, 1, 0, 0, 1, 0, testitulo);
		%VanhPRahaVS(testi2, &mvuosi, 1, 0, 1, 0, 0, testitulo);
		%VanhPRahaVS(testi3, &mvuosi, 1, 1, 0, 0, 0, testitulo);
		testi = &normpaiv * testi1 + &korpaiv75 * testi2 + &korpaiv90 * testi3;
	END;
	DO m = -9 TO 10 UNTIL(testi >= &praha);
		testitulo = (i * 100000 + j * 10000 + k * 1000 + m * 100);
		%VanhPRahaVS(testi1, &mvuosi, 1, 0, 0, 1, 0, testitulo);
		%VanhPRahaVS(testi2, &mvuosi, 1, 0, 1, 0, 0, testitulo);
		%VanhPRahaVS(testi3, &mvuosi, 1, 1, 0, 0, 0, testitulo);
		testi = &normpaiv * testi1 + &korpaiv75 * testi2 + &korpaiv90 * testi3;
	END;
	DO n = -9 TO 10 UNTIL(testi >= &praha);
		testitulo = (i * 100000 + j * 10000 + k * 1000 + m * 100 + n * 10);
		%VanhPRahaVS(testi1, &mvuosi, 1, 0, 0, 1, 0, testitulo);
		%VanhPRahaVS(testi2, &mvuosi, 1, 0, 1, 0, 0, testitulo);
		%VanhPRahaVS(testi3, &mvuosi, 1, 1, 0, 0, 0, testitulo);
		testi = &normpaiv * testi1 + &korpaiv75 * testi2 + &korpaiv90 * testi3;
	END;
	DO p = -9 TO 10 UNTIL(testi >= &praha);
		testitulo = (i * 100000 + j * 10000 + k * 1000 + m * 100 + n * 10 + p);
		%VanhPRahaVS(testi1, &mvuosi, 1, 0, 0, 1, 0, testitulo);
		%VanhPRahaVS(testi2, &mvuosi, 1, 0, 1, 0, 0, testitulo);
		%VanhPRahaVS(testi3, &mvuosi, 1, 1, 0, 0, 0, testitulo);
		testi = &normpaiv * testi1 + &korpaiv75 * testi2 + &korpaiv90 * testi3;
	END;

	&tulos = testitulo;

END;

DROP vahimm testi testi1 testi2 testi3 testitulo;
%MEND VanhRahaTuloS;
	



